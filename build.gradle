plugins {
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "5.2.0" apply false
    id "org.embulk.embulk-plugins" version "0.7.0"
    id "com.palantir.git-version" version "0.12.3"
}

repositories {
    mavenCentral()
}

group = "io.trocco"
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^v[0-9]+\.[0-9]+\.[0-9]+([.-][.a-zA-Z0-9-]+)?/) {
        vd.lastTag.substring(1)
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()
description = "Pardot input plugin for Embulk"

def embulkApiVersion = "0.10.43"
def embulkSpiVersion = "0.10.46"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencyLocking {
    lockAllConfigurations()
}

dependencies {
    compileOnly "org.embulk:embulk-api:${embulkApiVersion}"
    compileOnly "org.embulk:embulk-spi:${embulkSpiVersion}"

    implementation "org.embulk:embulk-util-config:0.3.4"
    implementation "org.embulk:embulk-util-timestamp:0.2.2"
    implementation project(path: ":shadow-pardot-helper", configuration: "shadow")

    testImplementation "junit:junit:4.13"
    testImplementation "org.mockito:mockito-core:1.10.19"
    testImplementation "org.embulk:embulk-api:${embulkApiVersion}"
    testImplementation "org.embulk:embulk-spi:${embulkSpiVersion}"
    testImplementation "org.embulk:embulk-core:${embulkSpiVersion}"
    testImplementation "org.embulk:embulk-deps:${embulkSpiVersion}"
    testImplementation "org.embulk:embulk-junit4:${embulkSpiVersion}"
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation"
}

embulkPlugin {
    mainClass = "org.embulk.input.pardot.PardotInputPlugin"
    category = "input"
    type = "pardot"
}

gem {
    from("LICENSE")
    authors = ["u110"]
    email = ["web.u.project@gmail.com"]
    summary = "${description}"
    homepage = "https://github.com/trocco-io/embulk-input-pardot"
    licenses = ["Apache-2.0"]
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {
            // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

