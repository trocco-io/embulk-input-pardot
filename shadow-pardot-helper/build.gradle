/*
 * A helper artifact to have the Google Ads client with some transitive dependencies relocated.
 *
 * This helper JAR is built separate from each plugin main JAR so that an impact of package relocation is
 * closed within the helper JAR, not to impact the plugin main JAR.
 */

apply plugin: "java"
apply plugin: "com.github.johnrengelman.shadow"

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/primenumber-dev/pardot-java-client")
        credentials {
            username = System.getenv("GITHUB_ACTOR") ?: System.getenv("GITHUB_USERNAME")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

group = "io.trocco"
version = "${rootProject.version}"
description = "A helper library for Pardot input plugin for embulk"

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    runtimeClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
    shadow {
        resolutionStrategy.activateDependencyLocking()
        transitive = false
    }
}

dependencies {
    // https://github.com/primenumber-dev/pardot-java-client
    implementation 'io.github.primenumber-dev:pardot-api-client:3.2.0-0.0.10'
}

// Relocate Guava and Jackson packages since they are incompatible from Embulk's.
shadowJar {
    mergeServiceFiles()
    relocate "com.google.common", "embulk.pardot.com.google.common"
    relocate "com.google.thirdparty.publicsuffix", "embulk.pardot.com.google.thirdparty.publicsuffix"
    relocate "com.fasterxml.jackson", "embulk.pardot.com.fasterxml.jackson"
}